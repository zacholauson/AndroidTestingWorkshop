apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'

android {
  compileSdkVersion 23
  buildToolsVersion "23.0.3"

  defaultConfig {
    applicationId "io.zacholauson.todo"
    versionCode 1
    versionName "1.0"

    targetSdkVersion 23
    minSdkVersion 19
  }

  sourceSets {
    androidTest.setRoot('src/test')
  }

  buildTypes {
    release {
      minifyEnabled false
    }
  }

  testOptions {
    unitTests.returnDefaultValues = true
  }
}

// Used to fix the discrepancy between Dagger's Guava version and Robolectric's Guava version
configurations.all { resolutionStrategy.force 'com.google.guava:guava:19.0-rc2' }

dependencies {
  final DAGGER_VERSION = '2.4'
  final ROBOLECTRIC_VERSION = '3.1-SNAPSHOT'

  compile group: 'com.android.support', name: 'appcompat-v7',    version: '23.4.0'

  // Used for dependency injection in Android components
  compile group: 'com.google.dagger',   name: 'dagger',          version: DAGGER_VERSION
  apt     group: 'com.google.dagger',   name: 'dagger-compiler', version: DAGGER_VERSION

  compile project(":TodoCore")

  // Test runner library
  testCompile        group: 'junit',       name: 'junit',        version: '4.12'
  androidTestCompile group: 'junit',       name: 'junit',        version: '4.12'

  // Assertion library
  testCompile        group: 'org.assertj', name: 'assertj-core', version: '1.7.1'
  androidTestCompile group: 'org.assertj', name: 'assertj-core', version: '1.7.1'

  // Mocking library
  testCompile        group: 'org.mockito', name: 'mockito-core', version: '1.10.19'
  androidTestCompile group: 'org.mockito', name: 'mockito-core', version: '1.10.19'

  // Used for unit testing Android components
  testCompile        group: 'org.robolectric', name: 'android-all',  version: '4.1.2_r1-robolectric-0'
  androidTestCompile group: 'org.robolectric', name: 'android-all',  version: '4.1.2_r1-robolectric-0'
  testCompile        group: 'org.robolectric', name: 'robolectric',  version: ROBOLECTRIC_VERSION
  androidTestCompile group: 'org.robolectric', name: 'robolectric',  version: ROBOLECTRIC_VERSION
  testCompile        group: 'org.robolectric', name: 'shadows-core', version: ROBOLECTRIC_VERSION
  androidTestCompile group: 'org.robolectric', name: 'shadows-core', version: ROBOLECTRIC_VERSION

  // Required by Robolectric for mocking Canvas
  testCompile         group: 'org.khronos', name: 'opengl-api', version: 'gl1.1-android-2.1_r1'
   androidTestCompile group: 'org.khronos', name: 'opengl-api', version: 'gl1.1-android-2.1_r1'
}

// prevent the "superClassName is empty" error for classes not annotated as tests
tasks.withType(Test) {
  scanForTestClasses = false

  include "**/*Test.class"
}